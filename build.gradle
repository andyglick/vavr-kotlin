plugins {
    id 'jvm-component'
    id 'java-lang'
    id 'maven'
    id 'jacoco'
    id 'com.bmuschko.nexus' version '2.3.1'
    id 'org.jetbrains.kotlin.jvm' version '1.1.4-3'
}

model {
    components {
        main(JvmLibrarySpec)
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

group = 'io.vavr'
version = '0.9.0-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre8', version: '1.1.4-3'
    compile group: 'io.vavr', name: 'vavr', version: '0.9.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    sourceSets sourceSets.main

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    project.test
}



modifyPom {
    project {
        name 'Vavr Kotlin'
        description 'Vavr integration for Kotlin'
        url 'http://vavr.io'
        inceptionYear '2017'

        scm {
            connection 'scm:git:git@github.com:vavr-io/vavr-kotlin.git'
            developerConnection 'scm:git:git@github.com:vavr-io/vavr-kotlin.git'
            url 'git@github.com:vavr-io/vavr-kotlin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }
    }
}
